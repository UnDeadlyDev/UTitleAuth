plugins {
    id 'java'
    // https://github.com/GradleUp/shadow
    id 'com.gradleup.shadow' version '9.2.1'
    // https://github.com/Minecrell/plugin-yml
    id 'net.minecrell.plugin-yml.bukkit' version '0.6.0'
}

group 'com.undeadlydev'
version '8.3.5'

bukkit {
    name = 'UTitleAuth'
    main = 'com.undeadlydev.UTA.Main'
    version = project.version
    apiVersion = '1.13'
    author = 'UnDeadlyDev'
    depend = ['AuthMe']
    softDepend = ['PlaceholderAPI', 'CMILib', 'FastLogin']
}

repositories {
    mavenLocal()
    // mavenCentral()
    // Instead of real maven central we use the maven mirror Paper uses, to ensure all the dependencies we want will
    // also be available to the library loader.
    maven { url = 'https://maven-central.storage-download.googleapis.com/maven2' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://maven.enginehub.org/repo/'}
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url = 'https://repo.codemc.io/repository/maven-snapshots/' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    // https://hub.spigotmc.org/stash/projects/SPIGOT/repos/spigot/browse
    compileOnly 'org.spigotmc:spigot-api:1.21.8-R0.1-SNAPSHOT'

    // https://github.com/PlaceholderAPI/PlaceholderAPI
    compileOnly 'me.clip:placeholderapi:2.12.2'

    compileOnly 'fr.xephi:authme:5.6.1-SNAPSHOT'

    // https://github.com/Bastian/bstats-metrics
    implementation 'org.bstats:bstats-bukkit:3.1.0'

    // https://github.com/CryptoMorin/XSeries
    implementation 'com.github.cryptomorin:XSeries:13.6.0'

    // https://github.com/TechnicallyCoded/FoliaLib
    implementation 'com.github.technicallycoded:FoliaLib:0.4.4'

    compileOnly files('libs/FastLoginBukkit.jar')
    compileOnly files('libs/CMILib1.5.6.9.jar')
}

shadowJar {
    archiveFileName = "${rootProject.name}-${version}.jar"

    def path = 'com.undeadlydev.UTA.libraries'
    minimize {
        exclude(dependency('com.github.technicallycoded:FoliaLib:.*'))
    }
    relocate 'org.bstats', path +'.bstats'
    relocate 'com.cryptomorin.xseries', path +'.xseries'
    relocate('com.tcoded', path + '.tcoded')
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

tasks.register('validateLibraryLoaderDeps') {
    doLast {
        def repoUrl = "https://maven-central.storage-download.googleapis.com/maven2"
        def configToCheck = configurations.library

        def externalDeps = configToCheck.allDependencies.findAll {
            it instanceof ExternalModuleDependency && it.version
        }

        def missing = []

        externalDeps.each { dep ->
            def groupPath = dep.group.replace('.', '/')
            def fullUrl = "${repoUrl}/${groupPath}/${dep.name}/${dep.version}/${dep.name}-${dep.version}.pom"
            def gav = "${dep.group}:${dep.name}:${dep.version}"

            try {
                def connection = new URL(fullUrl).openConnection() as HttpURLConnection
                connection.requestMethod = "HEAD"
                connection.connectTimeout = 5000
                connection.readTimeout = 5000

                if (connection.responseCode == 200) {
                    println "Found: ${gav}"
                } else {
                    println "Missing: ${gav} (HTTP ${connection.responseCode})"
                    missing << fullUrl
                }
            } catch (Exception e) {
                println "Error checking ${gav} - ${e.message}"
                missing << fullUrl
            }
        }

        if (!missing.isEmpty()) {
            println "The following dependencies were not found:"
            missing.each { println " - $it" }
            throw new GradleException("Some dependencies were not found in the repository.")
        } else {
            println "All dependencies found in ${repoUrl}."
        }
    }
}

processResources {
    inputs.property('version', project.version)
}